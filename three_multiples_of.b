=== layout =================================

0  A1   constant : counter
1  B1   variable : comparison(1)
2  B2   variable : comparison(2)
3  C1   variable : flag of "comparison"
4  C2   variable : flag of "threee multiples of"
5  C3   variable : flag of "display"
6  D1   variable : the hundred's place
7  D2   variable : the ten's place
8  D3   variable : the one's place
9  E1   Character code(space 32)
10 E2   Character code(! 33)
11 E3   Character code(\n 10)
12 F1   variable : flag of "new line"
13 F2   variable : counter(F1)
14 E4   Character code(: 58)
15 E5   Character code(( 40 & ) 41)

============================================

105 110 112 117 116 32 110 117 109 98 101 114 32 58 32

// setup
( A1 )>>>>( C2 )+++     // ste C2 3
<<<( B1 )++++[->>>>>>>>( E1 )++++++++>( E2 )++++++++<<<<<<<<<(B1)]     // set E1 32
>>>>>>>>>( E2 )+     // set E2 33
>( E3 )++++++++++     //set E3 10 
>>( F2 )++++++++++     //set F2 10
<<<<<<<<<<<<( B1 )++++++[->>>>>>>>>>>>>( E4 )++++++++++<<<<<<<<<<<<<( B1 )]  
>>>>>>>>>>>>>( E4 )--     // set E4 58
<<<<<<<<<<<<( B1 )++++[->>>>>>>>>>>>>( E5 )++++++++++<<<<<<<<<<<<<( B1 )]     //set E5 40 
<<( A1 )
>( B1 )++++++++++[-<( A1 )++++++++++>( B1 )]<( A1 )+++++.     // i 105 
+++++.     // n 110
++.     // p 112
+++++.     // u 117
-.     // t 116
>>>>>>>>>( E1 ).     // space 32( E1 )
<<<<<<<<<------( A1 ).     // n 110
+++++++.     // u 117
--------.     // m 109
-----------.     // b 98
+++.   // e 101
+++++++++++++.     // r 114
>>>>>>>>>>>>>>>( E5 ).     // ( 40
<<<<<<<<<<<<<<<( A1 )-----.     // m 109 
------------.     // a 97 
>( B1 )++[-<( A1 )++++++++++>( B1 )]<( A1 )+++.     // x 120
>>>>>>>>>( E1 ).     // space 32
>>>>>>++++++++++.     // 2 50
+++.     // 5 53
.     // 5 53
------------( E5 ).     // ) 41
<<<<<<( E1 ).     // space 32( E1 )
>>>>>( E4 ).     // : 58( E4 )
<<<<<( E1 ).     // space 32( E1 )
<<<<<<<<<( A1 ),     // input
>>>>>>>>>>>( F1 ).     // newline(10)
<<<<<<<<<<<( A1 )


// main
[
   ( A1 )>>>>>>>>( D3 )+     // add D3 1
   
   [<<<<<<<( B1 )+>( B2 )+>>>>>>( D3 )-]     // copy D3 to B1 and B2
   <<<<<<( B2 )[>>>>>>( D3 )+<<<<<<( B2 )-]     // copy B2 to D3
   ++++++++++     // set B2 10
   ========== branch
   <( B1 )[>( B2 )-<(B1)-]     // judge : B2 subtraction B1
      ========== B2 subtraction B1 == 0
      >>( C1 )+     // flag set
         ========== if
         <( B2 )[>( C1 )[-]<( B2 )[-]]
         >( C1 )[>>>>>( D3 )[-]<( D2 )+<<<<( C1 )-]     // " D3 = 0 "   " add D2 1 " 
         
   >>>>( D2 )[<<<<<<( B1 )+>( B2 )+>>>>>( D2 )-]     // copy D2 to B1 and B2
   <<<<<( B2 )[>>>>>( D2 )+<<<<<( B2 )-]     // copy B2 to D3
   ++++++++++     // set B2 10
   ========== branch
   <( B1 )[>( B2 )-<(B1)-]     // judge : B2 subtraction B1
      ========== B2 subtraction B1 == 0
      >>( C1 )+     // flag set
         ========== if
         <( B2 )[>( C1 )[-]<( B2 )[-]]
         >( C1 )[>>>>( D2 )[-]<( D1 )+<<<( C1 )-]     // " D2 = 0 "   " add D1 1 " 
         
   >>( C3 )+     // flag set
   ========== branch A
   <( C2 )-
   [<<<( B1 )+>( B2 )+>>( C2 )-]     // copy C2 to B1 and B2
   <<( B2 )[>>( C2 )+<<( B2 )-]     // copy B2 to C2
   >( C1 )+     // flag set 
      ========== C1 != 0
      <<( B1 )[>>( C1 )[-]<<( B1 )[-]]
         ========== if
         >>( C1 )[
		    >( C2 )+++   // set C2 3
		       ========== display
		       <<<( B1 )++++++     // set B1 6
		       [>>>>>( D1 )++++++++>( D2 )++++++++>( D3 )++++++++<<<<<<<(B1)-]
		       >>>>>( D1 ).
		       >( D2 ).
		       >( D3 ).
		       >>( E2 ).
		       
		       <<<<<<<<<( B1 )++++++  set B1 6
		       [>>>>>( D1 )-------->( D2 )-------->( D3 )--------<<<<<<<(B1)-]
		       
		       >>( C1 )[-]
		       >>( C3 )[-]
		       <<( C1 )
			]
   ========== branch B
   >>( C3 )[
      <<<<( B1 )++++++     // set B1 6
      [>>>>>( D1 )++++++++>( D2 )++++++++>( D3 )++++++++<<<<<<<(B1)-]
	  >>>>>( D1 ).
	  >( D2 ).
	  >( D3 ).
	  >( E1 ).
	  
	  <<<<<<<<( B1 )++++++  set B1 6
      [>>>>>( D1 )-------->( D2 )-------->( D3 )--------<<<<<<<(B1)-]
      >>>>( C3 )[-]
   ]
   
   >>>>( E1 ).
   
   >>>( F1 )+
   >( F2 )-
   [<<<<<<<<<<<<( B1 )+>( B2 )+>>>>>>>>>>>( F2 )-]     // copy F2 to B1 and B2
   <<<<<<<<<<<( B2 )[>>>>>>>>>>>( F2 )+<<<<<<<<<<<( B2 )-]     // copy B2 to F2
   >( C1 )+     // flag set
      ===== C1 != 0
	  <<( B1 )[>>( C1 )[-]<<( B1 )[-]]
	     ===== if
	     >>( C1 )[
		    >>>>>>>>>>( F2 )++++++++++
		    <<( E3 ).
		    <<<<<<<<( C1 )-
			]	        
   
   <<<( A1 )-
]  